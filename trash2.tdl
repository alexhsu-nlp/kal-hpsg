;;; PRN should have ref-ind only
non-local :+ [ PRN append-list ].

coord-phrase :+ 
  [ SYNSEM.NON-LOCAL.PRN.APPEND < #prn1, #prn2 >,
    LCOORD-DTR.SYNSEM.NON-LOCAL.PRN #prn1,
    RCOORD-DTR.SYNSEM.NON-LOCAL.PRN #prn2 ].

same-prn-binary-phrase := basic-binary-phrase & 
  [ SYNSEM [ L-PERIPH #periph,
             NON-LOCAL.PRN.APPEND < #prn1, #prn2 > ],
    ARGS < [ SYNSEM [ L-PERIPH #periph,
                      NON-LOCAL.PRN #prn1 ] ],
           [ SYNSEM [ L-PERIPH -,
                      NON-LOCAL.PRN #prn2 ] ] > ].

same-prn-unary-phrase := unary-phrase &
  [ SYNSEM.NON-LOCAL.PRN #prn,
    ARGS < [ SYNSEM.NON-LOCAL.PRN #prn ] > ].

4th-to-3rd-coref-index-unary-phrase := unary-phrase & 
  [ SYNSEM [ LOCAL.CONT.HOOK.INDEX.PNG [ PER 3rd, NUM #num ], 
             NON-LOCAL.PRN.LIST < #index > ], 
    ARGS.FIRST.SYNSEM.LOCAL.CONT.HOOK.INDEX ref-ind & #index & [ PNG [ PER 4th, NUM #num ] ] ].

4th-to-3rd-coref-xarg-unary-phrase := unary-phrase & 
  [ SYNSEM [ LOCAL.CONT.HOOK.XARG.PNG [ PER 3rd, NUM #num ], 
             NON-LOCAL.PRN.LIST < #index > ], 
    ARGS.FIRST.SYNSEM.LOCAL.CONT.HOOK.XARG ref-ind & #index & [ PNG [ PER 4th, NUM #num ] ] ].

;;; Note: this is fine because subjects in Kalaallisut has to be of [ HEAD noun ] , not +vj etc.
;;; hmm how about getting link in LOCAL.VAL.SUBJ rather than in this binary phrase? then it is not nonlocal
;;; not quite; it might be the case that NP contains 4th person possession
prn-head-subj-phrase := basic-head-subj-phrase & 
  [ SYNSEM.NON-LOCAL.PRN.LIST < >,
    HEAD-DTR.SYNSEM.NON-LOCAL.PRN.LIST < #index >, 
    NON-HEAD-DTR.SYNSEM.LOCAL.CONT.HOOK.INDEX #index ].